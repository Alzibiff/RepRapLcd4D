#platform "uLCD-32PT_GFX2"
#inherit "GfxInterface.4dg"
#inherit "SerialManager.4dg"
#inherit "GfxInterfaceConst.inc"
#inherit "Constant.inc"

var __X;
var __Y;

func TouchEvent(var x,var y)
    var i;
    var touched;
    __X:=x;
    __Y:=y;

    touched:=img_Touched(hndl,-1);

    if(WINDOW==W_MAIN)  //Window Main TouchEvent
        if(touched==iImage1 || touched==iImage2) //Axis Move
            for(i:=0; i<sizeof(AXIS_MOVE_TOUCH_REGION); i++)
                if( checkRegion( @ AXIS_MOVE_TOUCH_REGION[i] ) ) //Moves the axes depending on the region Touched
                    SerialPrintlnBuffer(AXIS_MOVE_TOUCH_ACTION[i]);
                    pause(50);
                    break;
                endif
            next
        else if(touched==iImage3) //SD Card
            WINDOW:=W_SDCARD;
            drawSDScreen();
        else if(touched==iWinbutton1) //Extrude Button
            //updateButtonExtrude(TRUE);
            SerialPrintBuffer("G91\nG1 E");
            SerialPrintNumber(str2w(str_Ptr(ex_setmm)));
            SerialPrintBuffer(" F");
            SerialPrintlnNumber(str2w(str_Ptr(ex_setmm_min)));
            SerialPrintlnBuffer("G90");
            //updateButtonExtrude(FALSE);
            pause(50);
        else if(touched==iWinbutton2) //Reverse Button
            SerialPrintBuffer("G91\nG1 E-");
            SerialPrintNumber(str2w(str_Ptr(ex_setmm)));
            SerialPrintBuffer(" F");
            SerialPrintlnNumber(str2w(str_Ptr(ex_setmm_min)));
            SerialPrintlnBuffer("G90");
        else if(touched==iWinbutton3) //Extruder Off
            SerialPrintlnBuffer("M104 S0");
        else if(touched==iWinbutton4) //Bed Off
            SerialPrintlnBuffer("M140 S0");
        else if(touched==iWinbutton5) //Extruder Set
            SerialPrintBuffer("M104 S");
            SerialPrintlnNumber(str2w(str_Ptr(ex_setTemp)));
        else if(touched==iWinbutton6) //Bed Set
            SerialPrintBuffer("M140 S");
            SerialPrintlnNumber(str2w(str_Ptr(bed_setTemp)));
        else if(touched==iWinbutton9 || touched==iWinbutton10) //Switch Extruder
            updateButtonSwitchEx();
        else //ALL "Button Set NUMBER"
            for(i:=0; i<sizeof(BUTTON_SET_NUMBER); i++)
                if(checkRegion( @ BUTTON_SET_NUMBER[i]))
                    initTrackbar(i);
                    updateTrackbarStatus(i);
                    initButtonFine();
                    updateButtonFine(FALSE);
                    break;
                endif
            next
        endif

    else if(WINDOW==W_EXTMM || WINDOW==W_EXTMM_MIN || WINDOW==W_EXTTEMP || WINDOW==W_BEDTEMP) //Window Button Set Number
        if(touched==iTrackbar1)
             updateTrackbarEvent(WINDOW,x);
             EN_TOUCH_MOVING:=TRUE;
             //pause(50);
        else if(touched==iWinbutton7) //Fine Plus
            ButtonFinePlusAction();
            pause(70);
        else if(touched==iWinbutton8) //Fine Minus
            ButtonFineMinusAction();
            pause(70);
        else if(!checkRegion( @ TRACKPAD_CONTAINER))
            remove_currentTrackpad();
            EN_TOUCH_MOVING:=FALSE;
            WINDOW:=W_MAIN; //Return to Windows Main
        endif
    else if(WINDOW==W_SDCARD) //Window SDCard Touch event
        if(checkRegion( @ WIN_SDCARD_CONTAINER))
        else
            WINDOW:=W_MAIN;
            gfx_RectangleFilled(0, 167, 319, 229,BLACK);
            drawGfxInterface();
        endif
    endif
endfunc

func TouchReleasedEvent(var x,var y)

endfunc

func checkRegion(var p1x,var p1y ,var p2x,var p2y)
    var ret := FALSE;
    if( __X >= p1x && __X <= p2x && __Y >= p1y && __Y <= p2y)
        ret := TRUE;
    endif
    return ret;
endfunc
